LevelTitle
════════════════════════════════════════════════════════════════════════

const fadeInStart  = 0
const fadeInEnd    = fadeInStart + fadeInFrames - 1

const flyInStart  = fadeInEnd - 1
const flyInEnd    = flyInStart + levelTitle.flyInFrames - 1

const holdStart   = flyInEnd + 1
const holdEnd     = holdStart + levelTitle.holdFrames - 1

const flyOutStart = holdEnd + 1
const flyOutEnd   = flyOutStart + levelTitle.flyOutFrames - 1


def drawDescription(α):
   draw_text({font: largeFont,
             text: "Level " + (level + 1) + "{font:font /" + numLevels + "}",
             pos: xy(lerp(-50, ½ SCREEN_SIZE.x, α), 90), x_align: "center",
             color: #f, outline: #0002, shadow: #0004, markup: true})
   
   draw_text({font: largeFont,
             text: levelDescription,
             pos: xy(lerp(SCREEN_SIZE.x + 50, ½ SCREEN_SIZE.x, α), 130),
             x_align: "center",
             color: #f, outline:#0002, shadow: #0})

   
enter
────────────────────────────────────────────────────────────────────────
reset_transform()
reset_clip()
reset_post_effects()



frame
────────────────────────────────────────────────────────────────────────
set_background(location.background)

if level > 0:
   drawScores()
else:
   // Show controls
   draw_text(instructionsFont,
            replace("[^][v][<][>] to move, (p) to pause", joy.prompt),
            xy(½ SCREEN_SIZE.x, SCREEN_SIZE.y - 10),
            #4, #0002, ∅, "center", "bottom")


if mode_frames ≤ fadeInEnd:
   // Fade in
   const α = (mode_frames - fadeInStart + 1) / fadeInFrames
   set_post_effects({motion_blur: 100% - α})
   
else if mode_frames ≤ flyInEnd:
   reset_post_effects()
   // Fly in the title
   const α = (mode_frames - flyInStart + 1) / levelTitle.flyInFrames
   drawDescription(α)
   
else if mode_frames ≤ holdEnd:
   const α = (mode_frames - holdStart + 1) / levelTitle.holdFrames
   drawDescription(1)

else if mode_frames ≤ flyOutEnd:
   const α = (mode_frames - flyOutStart + 1) / levelTitle.flyOutFrames
   drawDescription(1 + α)

else:
   // The Play mode will process the fade in over itself
   set_mode(Play) because "Animation end"


