GenerateBoard
════════════════════════════════════════════════════════════════════════
// This mode generates the board, but also displays an image first
// so that it does not look like the game has locked up when board
// generation runs slowly


enter
────────────────────────────────────────────────────────────────────────
clear_npc_message()


frame
────────────────────────────────────────────────────────────────────────
set_background(#000)

// Draw on the first few frames (needed in case we're running at low
// frame rate--ensure this is seen). Then, generate the board and go
// to play.

draw_text({
    text: "Generating board…",
    font: score_font,
    color: HIGHLIGHT_COLOR,
    pos: ½ SCREEN_SIZE})
    

if mode_frames > 3:
    const board_string = (
        if DEBUG.BOO or DEBUG.INTERMISSION then "GHOS BOOT FEAR HALE" else 
        (if DEBUG.MZK then "DEHS CKOT NOBSLEERT" else
        (if not player.has_achievement["Play Solo Match"] then TUTORIAL.FIRST_TIME_BOARD 
        else ∅)))
    player_reset_match(player, game_type, board_string)
    
    if DEBUG.INTERMISSION or DEBUG.EARN_XP:
        const n = if DEBUG.EARN_XP then 6 else 35
        for i < n:
            const word = random_value(player.board.word_list)
            if not player.already_found[word]:
                player_add_to_history(player, word, score_word(word))
                
    if DEBUG.EARN_XP:
        player.score = 700
                    
    if in_waiting_room:
        set_mode(WaitingRoom) because "No GLOBAL in progress"
    else:
        set_mode(Play)
    