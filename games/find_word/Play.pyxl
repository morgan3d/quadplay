Play
════════════════════════════════════════════════════════════════════════
def enter_freestyle():
    board_deselect_all(player.board, player)
    push_mode(Freestyle) because "Button"



def execute_remix():
    player_achieve(player, "Use Remix")
    play_sound({sound: remix_sound, volume: 50%, playback_rate: 200%})
    sort(player.remix_list, size_comparator)
    
    for word at w in player.remix_list:
        const points = score_word(word)
        player_add_to_history(player, word, points)
        player_spawn_score_animation(player, "Remix")
        
        // Per-word sound
        if w < 7: delay(play_points_sound, 7 w, points)
    
    --player.remix_uses_left
    player.remix_list = ∅
    
    
def enter_rotate_mode():
    push_mode(Rotate) because "button"
    
    

enter
────────────────────────────────────────────────────────────────────────
assert(not in_waiting_room)

play_sound(start_sound)
    
last_countdown_sound_time = now()
start_time = frame_utc_now

// Compute end_time
if game_type == GAME_TYPE.SOLO:
    end_time = frame_utc_now + (if player.has_achievement["Play Solo Match"] then 60 SOLO_PLAY_MINUTES else TUTORIAL.FIRST_PLAY_SECONDS)
else:
    assert(game_type == GAME_TYPE.GLOBAL)
    
    end_time = prev_global_game_utc_start_time() + GLOBAL_PLAY_SECONDS

    /*
    assert(
        end_time > frame_utc_now + GLOBAL_JOIN_MIN_REMAINING_TIME - 1 or 
        DEBUG.INTERMISSION,
        "The game should not end so soon")*/

set_background(
    if game_type == GAME_TYPE.GLOBAL 
    then GLOBAL_BACKGROUND_COLOR
    else SOLO_BACKGROUND_COLOR)

// Started more than 10s late, do not affect
// ratio stats
joined_late = (end_time - start_time < GLOBAL_PLAY_SECONDS - 10)

board_deselect_all(player.board, player)


frame
────────────────────────────────────────────────────────────────────────
const time_left = end_time - frame_utc_now   

// Disallow rotation if the mode is about to end
const allow_rotate = (60 time_left) > (90° / ROTATION_SPEED)

player_update(player, get_board_pos(), ∅, allow_rotate)

handle_buttons(
    ability_icon.freestyle, "Freestyle", enter_freestyle,
    ability_icon.remix, if player.remix_uses_left and size(player.remix_list) then execute_remix else ∅,
    aux_icon.rotate, enter_rotate_mode)

if time_left ≤ 20/60:
    // Make a few frames black at the end so that a player can
    // still trace a little further but can't see. This makes
    // it feel like they squeezed in one more word and weren't
    // "robbed" if in the process of selecting. Without this
    // it always feels like "I was just about to finish my word"
    draw_corner_rect(xy(0, 0), SCREEN_SIZE, #000, ∅, 100)
    
    
if time_left ≤ 0 or DEBUG.INTERMISSION:
    board_deselect_all(player.board, player)
    if game_type == GAME_TYPE.SOLO:
        set_mode(SoloSummary) because "SOLO and timer"
    else:
        set_mode(GatherGlobalResults) because "GLOBAL and timer"


leave
────────────────────────────────────────────────────────────────────────
play_sound(times_up_sound)
if game_type == GAME_TYPE.GLOBAL:
    player_achieve(player, "Play Global Match")
    
else if (game_type == GAME_TYPE.SOLO and
    player.has_achievement["Spell EAT"] and 
    player.has_achievement["Spell SEAT"] and 
    player.has_achievement["Spell TIDE"] and 
    player.has_achievement["Spell EXIST"]):
        
    player_achieve(player, "Play Solo Match")
    

// Accumulate the free mode points into the score
// now that they're locked in
for points in entry in player.history.free:       
    player.score += points
    

// Reset the scroll position because layout differs
// from the main game during intermission
player.history.scroll.offset = xy(0, 0)

// Sort words by final score
sort(player.history.list, "points", true)
sort(player.history.free, "points", true)
