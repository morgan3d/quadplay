todo("Misspelling bot")

/* Generate a stable set of bots with a range of abilities, and 
   produce their scores and word lists for the current `player.board`. 
   
   All players see the same bots doing the same things, but individual
   players might be able to see more of the bots based on what they
   have unlocked.
   
 */
def insert_bots(competitor_array, max_non_npc_bots):
    // Verify that the match has been constructed correctly
    assert(size(match.word_set) == size(match.word_list) and size(match.word_list) > 10)
    assert(match.word_set[match.word_list[0]] and match.word_set[match.word_list[1]] and match.word_set[last_value(match.word_list)])
    assert(contains(match.board_string, match.word_list[0][0]) and contains(match.board_string, match.word_list[1][0]))
    if DEBUG.BOTS: debug_print("------------------------\ninsert_bots()")
    
    // Used for determining which players are active
    const hour = utc_time().hour
    const minute = utc_time().minute
    
    // Shift everybody's ability by this amount
    const match_shift = make_random(match.seed)(-10%, 0%)
    
    /* If vocabulary is specified, the bot is limited to that vocabulary 
       (except for Moo).
       
       Returns the competitor. */
    def maybe_add_bot_competitor(descriptor):
        assert(descriptor and descriptor.sprite and descriptor.name)

        const competitor = {
            …make_competitor(),
            
            avatar: {
                // Bots face left
                sprite: evaluate_constant_expression(descriptor.sprite).x_flipped,
                name: descriptor.name,
                bot: true,
                appears_human: descriptor.appears_human default false,
                id: "BOT_" + descriptor.name},
            
            
            enemy: descriptor.npc and descriptor.npc.evil,
            
            // NPC properties:
            npc: if not descriptor.npc then {} else {
                importance: descriptor.npc.importance,
                expert_tile: descriptor.npc.expert_tile,
                expert_prompt: descriptor.npc.expert_prompt,
                
                play_sound_args: if descriptor.npc.sound then evaluate_constant_expression(descriptor.npc.sound) else ∅,
            }}
    
        competitor.avatar.color = avatar_compute_color(competitor.avatar.sprite)
        competitor.avatar.level = descriptor.level default clamp(⌈140 descriptor.base_ability^(1.3)⌉, 1, 40)
        
        const bot_rng = make_random(descriptor.name + match_shift)

        // Make stable how good this player is overall, and then add noise per game.
        // Apply that as a fraction of all words possible
        const find_percentage = min(30%, bot_rng(12%^⅓, 30%^⅓)³ + descriptor.base_ability)
        const words_found = clamp(⌊size(match.word_list) * find_percentage⌋, 5, 55)
        const short_word_preference = descriptor.short_word_preference
        
        // Is this particular competitor available right now?
        if ((descriptor.last_hour > descriptor.first_hour and
            (hour < descriptor.first_hour or hour > descriptor.last_hour)) or
        
            // Wraparound hours case
            (descriptor.last_hour ≤ descriptor.first_hour and
             hour > descriptor.last_hour and
             hour < descriptor.first_hour)):
                
            if DEBUG.BOTS: debug_print("Bot " + descriptor.name + " is not available now")
            return
        
        if DEBUG.BOTS: debug_print("Bot " + descriptor.name + ":")
        if DEBUG.BOTS: debug_print("  gathering up to " + words_found + " words")

        // Make a word list for this competitor and score it.
        // Higher chance of discovering shorter words via rejection sampling.
        
        const word_set = {}
        
        // Add word and its substrings to word_set if they are not sensitive
        def maybe_add_word(word, force default false):
            assert(word)
            if DEBUG.BOTS: debug_print("  considering " + word)
                        
            // Accept words that this competitor hasn't found with
            // decreasing probability as the word gets longer than 3 letters.
            // Players with higher base percentages also are better at long words.
            // We check word_set[word] at the start as an optimization; it would be
            //
            // fine to eliminate that as we're accumulating a set
            if (not word_set[word] and
                (force or
                     bot_rng() ≤ (3 descriptor.base_ability + 45%) / pow(max(size(word) - 2, 1), 15% + short_word_preference))):

                // If a word is found, all substrings are found.
                // Ensure that bots do not find sensitive words or
                // words that are not in their dictionary
                if DEBUG.BOTS: debug_print("  found " + word)
                for 3 ≤ len ≤ size(word):
                    const subword = slice(word, 0, len)
                    
                    if (
                        // Haven't already found on some other search branch (performance optimization)
                        not word_set[subword]
                    
                        // Actually a word in the board (performance optimization; we check the dictionary again below)
                        and match.word_set[subword]
                        
                        // Not a BAD word
                        and not sensitive[lowercase(q_decode(subword))]
                        
                        // Is a word in THIS bot's dictionary (vs. the full dictionary)
                        and dictionary[descriptor.dictionary default "common"][subword]):
                            
                        word_set[subword] = 1


        /* Special case AI for the caf bot. `word_set` is the output found words */
        def caf_ai():
            // Randomly choose a fantastic word 10% of the time
            if bot_rng() < 10% or DEBUG.CAF:
                const word = match.word_list[random_integer(max(0, size(match.word_list) - 6), size(match.word_list) - 1, bot_rng)]
                if word: maybe_add_word(word, true)
                    
            // Look for short cow words. These can't be sensitive
            for word in BOT.MOO.descriptor.vocabulary:
                if size(word) ≤ 3 and match.word_set[word]:
                    maybe_add_word(word, true)
        
            // Find all words with "CAF" as a substring, of any length
            for word in match.word_list:
                if contains(word, "CAF"):
                    maybe_add_word(word, true)
                    
            if size(word_set) == 0 and (bot_rng() < 50%):
                // caf has no words. Try to find another word so that caf doesn't complain too often
                for word in match.word_list:
                    if size(word) == 3:
                        // Add this word and exit (return value is ignored)
                        return maybe_add_word(word, true)
    
        def expert_npc_ai():
            for word in match.word_list:
                for tile in competitor.npc.expert_tile:
                    if contains(word, tile) and size(word_set) < words_found:
                        maybe_add_word(word, true)
                    

        // Special case for bots with a vocabulary, who always find *all* of their words
        for word in (descriptor.vocabulary default []):
            if match.word_set[word]:
                maybe_add_word(word, true)
                    
        let tries = 0        
        if descriptor == BOT.CAF.descriptor:
            // Special case for caf, who sometimes finds a great word, finds all
            // three-letter cow words, and with a low probability finds a terrible word
            caf_ai()
            
        else if descriptor.npc and descriptor.npc.expert_tile:

            expert_npc_ai()
            
        else if not descriptor.vocabulary or descriptor == BOT.MOO.descriptor:
            // Default AI + Moo's short words
            //
            // Pick up a bunch of cheap words first. For Moo these must be 3 letters.
            // For other bots they can be 3 or 4 letters
            const short_word_length = if descriptor == BOT.MOO.descriptor then 3 else 4
            
            if DEBUG.BOTS: debug_print("  looking for short words")
            while size(word_set) < clamp(2 short_word_preference, 10%, 90%) * words_found and tries < 2 words_found:
                ++tries
                const word = random_value(match.word_list, bot_rng)
                if size(word) ≤ short_word_length:
                    maybe_add_word(word)
    
            // Bots without a fixed vocabulary now look for random words
            if not descriptor.vocabulary and descriptor ≠ BOT.CAF.descriptor:
                if DEBUG.BOTS: debug_print("  looking for random words")
                tries = 0
                while size(word_set) < words_found and tries < 2 words_found:
                    ++tries
                    maybe_add_word(random_value(match.word_list, bot_rng))

        // Convert the set of words into a word list
        for word in keys(word_set):
            push(competitor.word_list, {word: word, points: score_word(word)})

        for list in [competitor.word_list, competitor.freestyle_word_list]:
            sort(list, "points", true)
            for points in entry in list:
                competitor.score += points

        // Only add bots if they found a word
        if descriptor.allow_zero_score or size(competitor.word_list) or descriptor == BOT.VIL.descriptor:
            return push(competitor_array, competitor)
        else if DEBUG.BOTS:
            debug_print("  left because they couldn't find words")

    
    // Add random bots based on time of day
    let c = 0
    while size(competitor_array) < max_non_npc_bots and BOT_DESCRIPTOR_ARRAY[c]:
        maybe_add_bot_competitor(BOT_DESCRIPTOR_ARRAY[c])
        ++c
    

    // Moo is a special case
    // ALWAYS insert Moo if the player has the See Moo ability. Otherwise insert Moo if
    // it is an odd 10 minute interval right now. Process Moo after other bots
    // so that players who don't see Moo don't have their other results affected
    if player.has_ability["See Moo"] or (⌊minute / 10⌋ mod 2) or DEBUG.CAF:
        maybe_add_bot_competitor(BOT.MOO.descriptor)
    
        // caf only shows up if Moo is also there
        if (player.has_ability["See caf"] and ((hour + ⌊minute / 7⌋) mod 2 == 0)) or DEBUG.CAF:
            maybe_add_bot_competitor(BOT.CAF.descriptor)

    if (player.has_ability["See VIL"] and _VIL_is_online(utc_time().day, hour, minute)) or DEBUG.VIL:
        maybe_add_bot_competitor(BOT.VIL.descriptor)

    // Boo arrives when at least one of their words is available
    maybe_add_bot_competitor(BOT.BOO.descriptor)
    
    const expert_array = [
        {
            descriptor: BOT.MZK.descriptor,
            ability: "See Mz. Kiwi"},
        
        {
            descriptor: BOT.DRQ.descriptor,
            ability: "See Dr. Quandary"}]
        
    for descriptor, ability in expert in expert_array:
        if player.has_ability[ability]:
            const bot = maybe_add_bot_competitor(descriptor)
            // Cheat their scores down so that they aren't intimidating
            if bot and bot.score > 250:
                bot.score = round(lerp(bot.score, 200, 80%))
        
    if DEBUG.AURA:
        maybe_add_bot_competitor(BOT.aura_test_bot.descriptor)



/* Letter expert NPC's `auto_close_condition()` callback for `set_npc_message()`.

   `args = {tile, hint}`*/
def _hinted_word_found_by_player(args):
    for word in keys(player.already_found):
        if word_to_hint_text(word, args.tile) == args.hint:
            return true
    return false


/* If the npc has a good in-game suggestion, make it with the npc's importance level.
   All npcs can make suggestions and only the best one will win. 
   
   Any bot with an "expert_tile" property will try to suggest from their vocabulary.
   */
def npc_maybe_make_suggestion(bot)
& if bot.npc.expert_tile
& with npc in bot
& if player.has_ability[npc.required_ability]:
    
    // Choose an 4+ tile word, prefering ones that the player
    // has never spelled themselves. This encourages the player to
    // learn and get the shorter words quickly so that the NPC will
    // help them with longer ones
    for j < size(bot.word_list):
        // Working backwards because word lists are sorted from best to worst.
        const word = bot.word_list[size(bot.word_list) - j - 1].word
 
        // Iterate through all tiles that this npc knows. If multiple ones hit,
        // then they will prefer the earlier ones
        for tile in npc.expert_tile:
        
            // Because a bot must get sub-strings of their expertise words en route to their word,
            // we must check to make sure this word is actually in their expertise. We also
            // prefer words that the player has not themselves seen before, but not exclusively
            if size(word) ≥ 4 and contains(word, tile) and (ξ < 50% or not player.vocabulary[word]) and not player.already_found[word]:
                const data = {tile: tile, hint: word_to_hint_text(word, tile)}
                
                // The player may have not found the word the bot is suggesting, but MAY have found a different
                // word that matches the hint pattern. In that case, keep looking for a good hint
                if not _hinted_word_found_by_player(data):
                    return set_npc_message(
                        bot.avatar, 
                        npc.expert_prompt[tile] + data.hint, 
                        npc.importance, 
                        npc.play_sound_args, 
                        ∅, 
                        _hinted_word_found_by_player, 
                        data,
                        npc.play_sound_args)



def _VIL_is_online(day, hour, minute):
    // Can only see VIL at higher levels and on occasional half hours,
    // with the frequency increasing
    return player.has_ability["See VIL Always"] or (((day + hour) mod 2 == 0) and
        (if player.has_ability["See VIL More Frequently"] then true else
                (⌊(day + hour + minute) / 10⌋ mod
                    (if player.has_ability["See VIL Frequently"] then 3 else 6) == 0)))
        

if DEBUG.VIL:
    debug_print("\nVIL's Schedule:")
    let online = 0
    let total = 0
    for hour < 23:
        for m < 12:
            const minute = 5 m
            ++total
            let text = ""
            if _VIL_is_online(0, hour, minute):
                ++online
                text = "  ON"
            debug_print(format_number((60 hour + minute) * 60, "clock24"), text)
    debug_print("VIL is online " + format_number(online / total, "%") + "\n")





