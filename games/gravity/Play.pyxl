Play
════════════════════════════════════════════════════════════════════════

/* For showing instructions. The order of controls in the display is
   sorted each frame based on their costs. */
const CONTROL_ARRAY = [
    {
        text: "[<][>] TURN",
        cost: 0,
        // Property on a ship that is true when this feature
        // can be used
        key:  "turn_enabled"
    },
    {
        text: "(a) FIRE",
        cost: ENERGY_TORPEDO,
        key:  "torpedo_enabled"    
    },
    {
        text: "(b) THRUST", 
        cost: (ENERGY_THRUST - ENERGY_RECHARGE) * THRUST_SECONDS * 60,
        key:  "thrust_enabled"   
    },
    {
        text: "(e) SHIELD",
        cost: (ENERGY_SHIELD - ENERGY_RECHARGE) * SHIELD_SECONDS * 60,
        key: "shield_enabled"
    }]


def color_string(color):
    return "rgb(" + color.r + "," + color.g + "," + color.b + ")"
    

// advantage = 100% is normal
def spawn_ship(name, i, sprite, gamepad, total_ships, phase, advantage default 100%):

    const θ = 180° + 360° (i / total_ships) + 15° ξ + phase

    // Enlarge with the world scale, but not quite as much
    const scale = 1.2 sqrt(0.5 ARENA_SCALE - 0.5)
    
    // Adjust the orbit for large and double suns
    const orbit = (15 sun_array[0].scale.x² + (if sun_array[0].scale.x < 1 then 90 else 56)) * ARENA_SCALE
    
    assert(advantage > 0 and advantage < 10)
    
    push(ship_array,
        push(entity_array,
            make_entity({
                name: name,
                pos: orbit * angle_to_xy(θ),
                vel: 0.9 angle_to_xy(θ + 90°),
                angle: θ + 100°,
                drag: 0,
                density: SHIP_DENSITY,
                
                // Counts up when at full energy
                death_blossom_charge: 0,
                
                // After the first four games, start giving an advantage to losing players on recharge
                death_blossom_recharge_rate: advantage,
                
                // Give an advantage on recharge to losing players
                energy_recharge_rate: ENERGY_RECHARGE * advantage,
                
                // Hit box (before scale)
                size: xy(30, 30) / scale,
                
                // Sprite size
                scale: scale,
                shape: "disk",
                index: i,
                override_color: gamepad.player_color,
                sprite: sprite,
                
                gamepad: gamepad,
                
                energy: ENERGY_MAX,
                z: 1,
                
                // Features enabled
                shield_enabled: true,
                thrust_enabled: true,
                torpedo_enabled: true,
                
                explode_sound: ship_explode_sound,
                
                thrust_audio: play_sound({
                    sound: thrust_sound, 
                    volume: 200%,
                    stopped: true}),
                    
                shield_audio: play_sound({
                    sound: shield_sound, 
                    volume: 200%,
                    loop: true,
                    stopped: true}),
                    
                child_array: [
                    // White glowing core
                    make_entity({
                        sprite: sprite,
                        density: 0,
                        pos_in_parent: xy(if sprite == ship_sprite[1] or sprite == shapes.rocket then -1 else -3, 0),
                        scale_in_parent: 70% xy(1, 1)})]},
                
                // child_table
                {
                    exhaust: make_entity({
                        sprite: shapes.cone,
                        density: 0,
                        opacity: 0%,
                        pos_in_parent: xy(-32, 0),
                        scale_in_parent: xy(-130%, 30%),
                        z_in_parent: -0.4,
                        override_color: #FF0}),
                    
                    shield: make_entity({
                        z_in_parent: -0.7,
                        density: 0,
                        pos_in_parent: xy(-2, 0),
                        scale_in_parent: xy(150%, 130%),
                        sprite: shapes.hexagon,
                        opacity: 0%,
                        override_color: gamepad.player_color})})))
    


// When to go back to title
let end_frame



enter(num_players, num_bots, score_array)
────────────────────────────────────────────────────────────────────────
assert(num_players ≥ 1)

end_frame = ∞

const num_ships = num_players + num_bots
ARENA_SCALE = 2.5 + 0.5 (min(4, num_ships) - 1)

// No handicaps in single-player mode
const max_score = if num_players == 1 then score_array[0] else max(…score_array)

// Spawn ships. Rotate the pattern to change starting conditions slightly
// while making it easy to recognize initial positions
const phase = 30° random(-1, 1)
for i < num_players:
    const advantage = min(250%, max(4, max_score + 2) / max(4, score_array[i] + 2))
    spawn_ship("P" + (i + 1), i, ship_sprite[i], gamepad_array[i], num_ships, phase, advantage)

for i < num_bots:
    spawn_ship("bot" + (i + 1), i + num_players, shapes.rocket, make_bot_gamepad(#666), num_ships, phase)



frame
────────────────────────────────────────────────────────────────────────
set_post_effects({bloom: BLOOM, afterglow: AFTERGLOW})

preserving_transform:
    // Move the camera so that the sun is not at the center
    set_camera({
        zoom: 100% / ARENA_SCALE,
        pos: (1 - smoothstep(0, 1, clamp(120% - 0.004 min(mode_frames, end_frame - mode_frames), 0%, 100%))) * xy(150 planet.orbit_side, 0)})
        
    iterate(star_array, draw_point)

    iterate(entity_array, update_entity)
    
    iterate_pairs(entity_array, collision_callback)
    
    iterate(sun_array, update_and_draw_sun)    
    update_and_draw_planet(planet)    
    particles_update_and_draw(particle_array)


// Instructions
let num_player_ships = 0
for energy, gamepad, index, death_blossom_charge in ship in ship_array:
    // Bots have to go through this loop to update their 
    // status indicator dots, but they do not write to the screen.
    
    const placement = 2 xy(gamepad.index mod 2, 1 - ⌊½ gamepad.index⌋) - 1 

    let gauge = resized("", ⌊energy * ENERGY_GAUGE_BOXES / ENERGY_MAX⌋, "◼")
    if placement.x < 0:
        gauge += "{<enable> ◼}"
    else:
        gauge = "{<enable> ◼}" + gauge 

    
    // Sort by increasing cost, reversing for player 1 whose instructions
    // are right aligned
    sort(CONTROL_ARRAY, "cost", placement.x > 0)
    
    let control_text = ""
    for text, cost, key in control at c in CONTROL_ARRAY:
        if energy ≥ cost:
            ship[key] = true
            control_text += "{<enable> " + text + "}"
        else:
            ship[key] = false
            control_text += text
            
        if c < size(CONTROL_ARRAY) - 1:
            control_text += "  "
            
    
    let blossom_text = " "
    if death_blossom_charge == DEATH_BLOSSOM_CHARGE_MAX:
        blossom_text = (if mode_frames mod 20 < 9 then "{color: #aaa" else "{<enable>") + " (q) DEATH BLOSSOM}"
    
    if placement.y > 0:
        control_text += "{br}" + gauge + "{br}" + blossom_text
    else:
        control_text = blossom_text + "{br}" + gauge + "{br}" + control_text

    control_text = replace(control_text, 
            {…gamepad.prompt,
                "<enable>": "color:" + color_string(65% gamepad.player_color)})
    
    if gamepad.status ≠ "bot":
        ++num_player_ships
        draw_text({
            font: font,
            pos: ½ placement * (SCREEN_SIZE - xy(5, 20)) - xy(0, 1),
            x_align: placement.x,
            y_align: 0,
            text: control_text,
            markup: true,
            z: -1,
            color: 25% gamepad.player_color,
            outline: 15% gamepad.player_color})


if ((num_player_ships == 0 or 
     (num_player_ships == 1 and size(ship_array) == 1)) and
    end_frame == ∞):
    // Game over
    end_frame = mode_frames + GAME_OVER_FRAMES


if mode_frames > end_frame:
    const score = load_local("score") default [0, 0, 0, 0]
    resize(score, 4)
    
    for thrust_audio, shield_audio in ship in ship_array:
        stop_audio(thrust_audio)
        stop_audio(shield_audio)
        ++score[ship.index]
    
    save_local("score", score)
        
    set_mode(Title) because "Ship destroyed"


leave
────────────────────────────────────────────────────────────────────────
remove_frame_hooks_by_mode(Play)
