/*
 quad://scripts/particle.pyxl

 @meta {license: "Copyright 2020 Morgan McGuire, Open source under the MIT License, https://opensource.org/licenses/MIT"}
 */

def make_particles(time_rate default 100%):
    const particle_array = []
    // Change this to make particles move faster or slower globally
    particle_array.time_rate = 100%
    return particle_array

    
def particles_spawn(particle_array, pos, vel, color, z, frames):
    assert(is_number(z) and not is_NaN(z))
    // Create a single moving particle and add it to the global array
    const p = push(particle_array, {pos: xy(pos), vel: xy(vel), color: rgba(color), z: z, frames: frames})

    // Internal hack
    // deep_immutable_clone() is an unsupported API that makes the cloned value unchangeable,
    // which for colors embeds a secret field that renders faster. We want this
    // secret field, so extract it.
    p.color["$color"] = deep_immutable_clone(color)["$color"]
    return p

   
def particles_spawn_from_map_ws_coord(
        particle_array,
        map,
        pos,
        z,
        max_frames          default 40,
        z_offset            default ½,
        spread_center       default pos,
        min_spread_speed    default ½,
        max_spread_speed    default 1, 
        common_dir          default xy(0, 0),
        min_common_speed    default 0,
        max_common_speed    default 0,
        common_vel          default xy(0, 0),
        density             default 3):
    
    if map.map and (pos == nil):
        pos = map.pos
        z = map.z
        max_frames = map.max_frames default 40
        z_offset = map.z_offset default ½
        spread_center = map.spread_center default pos
        min_spread_speed = map.min_spread_speed default ½
        max_spread_speed = map.max_spread_speed default 1 
        common_dir = map.common_dir default xy(0, 0)
        min_common_speed = map.min_common_speed default 0
        max_common_speed = map.max_common_speed default 0
        common_vel = map.common_vel default xy(0, 0)
        density = map.density default 3
        map = map.map

    // Make a fake entity
    const entity = {
        z: z,
        pos: transform_map_space_to_ws(map, floor(transform_ws_to_map_space(map, pos))) + 0.5 map.sprite_size,
        sprite: get_map_sprite_by_ws_coord(map, pos, z),
        scale: xy(1, 1),
        angle: 0,
        offset: xy(0, 0),
        vel: xy(0, 0)
    }

    if not entity.sprite: return
        
    particles_spawn_from_entity(particle_array,
        entity, max_frames, z_offset, spread_center,
        min_spread_speed, max_spread_speed, common_dir,
        min_common_speed, max_common_speed, common_vel, density)



/* Convert an entity's sprite into a particle system.
   Can also be called as spawn_particles_from_entity(particle_array, args) */
def particles_spawn_from_entity(
        particle_array,
        entity,
        max_frames,
        z_offset            default ½,
        spread_center       default entity.pos,
        min_spread_speed    default 0,
        max_spread_speed    default 0, 
        common_dir          default xy(0, 0),
        min_common_speed    default ½,
        max_common_speed    default 1,
        common_vel          default xy(0, 0),
        density             default 3):

    if entity.entity and (max_frames == nil):
        // Overloaded named argument version
        max_frames = entity.max_frames default 40
        z_offset = entity.z_offset default ½
        spread_center = entity.spread_center default entity.entity.pos
        min_spread_speed = entity.min_spread_speed default 0
        max_spread_speed = entity.max_spread_speed default 0
        common_dir = entity.common_dir default xy(0, 0)
        min_common_speed = entity.min_common_speed default ½
        max_common_speed = entity.max_common_speed default 1
        common_vel = entity.common_vel default xy(0, 0)
        density = entity.density default 3
        entity = entity.entity
    else:
        if max_frames == nil:
            max_frames = 40
        
    assert(entity, "entity must not be nil")

    const sprite = entity.sprite
    assert(sprite, "entity does not have a sprite in spawn_particles_from_entity()")
    
    const z = entity.z + z_offset
    const center = ½ (sprite.size - 1)
    const U = angle_to_xy(entity.angle)
    const V = perp(U)
   
    for y < sprite.size.y:
        for x < sprite.size.x:
            const sprite_pos = xy(x, y)
            const color = get_sprite_pixel_color(sprite, sprite_pos)
            
            // Entity space position
            const es_pos = (sprite_pos - center) * entity.scale
            
            // Draw space position
            const pos = entity.pos + U * es_pos.x + V * es_pos.y
            
            for i < density:
                let vel = entity.vel * random(¼, ¾) + common_vel            
                vel += direction(pos - spread_center) * random(min_spread_speed, max_spread_speed)
                vel += common_dir * random(min_common_speed, max_common_speed)
                particles_spawn(particle_array, pos, vel, color, z, lerp(30%, 100%, ξ²) * max_frames)
    

// You can register this as a frame hook, or explicitly run it once per frame.
// All arguments except the last are ignored. The last is the particle_array.
def particles_update_and_draw(...arg):

    const particle_array = last_value(arg)
    
    // There are a LOT of particles, so avoid the cost of a WITH statement
    // here.
    //
    // Also, particles will render fastest if they are kept grouped by z
    // value, so we preserve the order of the array when removing elements
    // by always copying elements during iteration to close gaps, amortizing
    // the cost of in-order removal.

    const rate = particle_array.time_rate default 100%
    
    const N = size(particle_array)
    
    // Place to copy the next element to
    let dst = 0

    // Iterate over all particles. src == dst and no actual copy happens
    // until they start being removed and these separate
    for src < N:
        const particle = particle_array[src]
        if src > dst: particle_array[dst] = particle
      
        particle.frames -= rate
        if particle.frames ≥ 0:
            // Fade out near end of life
            if particle.frames < 20 and (particle.frames bitand 1):
                const c = particle.color["$color"]
                if c > 0x1000:
                    particle.color["$color"] = SUB(c, 0x1000) bitor 0
    
            // Integrate
            const pos = particle.pos
            XY_MAD_S_XY(particle.vel, rate, pos, pos)
            draw_point(pos, particle.color, particle.z)
    
            // Preserve this element
            ++dst
         
    // Remove extra elements
    if dst < N: resize(particle_array, dst)
