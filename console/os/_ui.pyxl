const $system_window_background = #135
const $system_window_border     = #fff
const $system_prompt_color      = #4DF
const $system_prompt_shadow     = #0006

const $system_selected_color    = $system_prompt_color
const $system_unselected_color  = #b

const $online_name_length       = 7
const $max_recent_hosts         = 3

/* Chose the appropriate font for this resolution */
def $system_font():
    if VIEW_ARRAY[0].size.y <= 64:
        return $font5
    else if VIEW_ARRAY[0].size.y <= 180:
        return $font8
    else:
        return $font9
    

/* Draws text with the extended drop shadow and pulsing for menus */
def $draw_system_menu_text(args, selected):
    const font = $system_font()
    
    // Used for pulsing selected values
    const bright = 0.5 (1 + cos(mode_frames / 9))

    draw_text(extended(args, {
        font: args.font default font,
        pos: args.pos + (if selected then xy(0, 2) else xy(0, 1)),
        color: (if selected then 50% $system_prompt_shadow else $system_prompt_shadow),
        x_align: args.x_align default "left",
        y_align: args.y_align default "baseline"
    }))
    
    return draw_text(extended(args, {
        font: args.font default font,
        color: (if selected then lerp($system_selected_color, #fff, 50% bright) else $system_unselected_color),
        outline: (if selected then rgba(1, 1, 1, 25% bright) else nil),
        x_align: args.x_align default "left",
        y_align: args.y_align default "baseline"
    }))
